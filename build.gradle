plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta4'

    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'me.typical'
version = '1.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "codemc-repo"
        url = "https://repo.codemc.org/repository/maven-public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "xenondevs"
        url = "https://repo.xenondevs.xyz/releases"
    }
    maven {
        name = "tcoded-releases"
        url = "https://repo.tcoded.com/releases"
    }
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "stellardrift"
        url = "https://repo.stellardrift.ca/repository/maven-public/"
    }
}

dependencies {
    // Paper API (1.20+)
    compileOnly("io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT")

    // Brigadier (required at compile time for CommandAPI; server provides at runtime)
    compileOnly("com.mojang:brigadier:1.0.18")

    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.36")
    annotationProcessor("org.projectlombok:lombok:1.18.36")
    implementation("io.github.projectunified:uni-item-all:2.5.0")
    // Database dependencies (shaded)
    compileOnly("org.xerial:sqlite-jdbc:3.51.2.0")
    compileOnly("com.zaxxer:HikariCP:6.2.1")
    compileOnly("org.mariadb.jdbc:mariadb-java-client:3.5.3")

    // Command API (shaded)
    implementation("dev.jorel:commandapi-paper-shade:11.1.0")

    // Config & FoliaLib (shaded)
    implementation("de.exlll:configlib-yaml:4.8.0")
    implementation("com.tcoded:FoliaLib:0.5.1")

    // External plugin APIs (compile-only)
    compileOnly("net.milkbowl.vault:VaultAPI:1.7")
    compileOnly("me.clip:placeholderapi:2.11.6")
}

tasks {
    build {
        dependsOn shadowJar
    }
}

shadowJar {
    archiveClassifier.set('')
    manifest {
        attributes('paperweight-mappings-namespace': 'mojang')
    }

    // Relocate all shaded dependencies
    relocate 'de.exlll.configlib', 'me.typical.lib.configlib'
    relocate 'com.tcoded.folialib', 'me.typical.lib.folialib'
    relocate 'dev.jorel.commandapi', 'me.typical.lib.commandapi'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
